# author: Wei Xinpeng
# student ID: 519021910888

# Execution begin at address 0
    .pos 0
    irmovq stack, %rsp
    call main
    halt

    .align 8
ele1:
    .quad 0x00a
    .quad ele2
ele2:
    .quad 0x0b0
    .quad ele3
ele3:
    .quad 0xc00
    .quad 0

main:
    irmovq ele1, %rdi
    call rsum_list
    ret

# ls in %rdi
rsum_list:
    pushq %rbx
    xorq %rax, %rax # val = 0
    andq %rdi, %rdi
    je return
    mrmovq (%rdi), %rbx # val in rbx
    mrmovq 8(%rdi), %rdi
    call rsum_list
    addq %rbx, %rax # val = rest + val
return:
    popq %rbx
    ret

# Stack starts here and grows to lower addresses
    .pos 0x200
stack:
